plugins {
    id 'java-library'
    id 'maven-publish'
}

description = 'Codex Particle VFX'

// select one source-code (JDK) option
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = property('group')?: 'com.github.capdevon'
version = property('version')?: '0.0.1'

// select one version of the Engine:
ext.jmeVersion = '3.6.1-stable'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // runtime options:
        args = []
        classpath sourceSets.main.runtimeClasspath
        //debug true
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xbatch'
        //jvmArgs '-Xms512m', '-Xmx512m'
        //jvmArgs '-XX:+PrintCompilation'
        //jvmArgs '-XX:+UseConcMarkSweepGC'
        jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // lib dependencies
    implementation 'org.jmonkeyengine:jme3-core:' + jmeVersion
    implementation 'com.github.stephengold:Heart:8.8.0'

    // test/demo dependencies
    testImplementation 'org.jmonkeyengine:jme3-desktop:' + jmeVersion
    testImplementation 'org.jmonkeyengine:jme3-effects:' + jmeVersion
    testImplementation 'org.jmonkeyengine:jme3-terrain:' + jmeVersion
    testRuntimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jmeVersion // JMonkeyEngine v3.6 only!

    testImplementation 'com.github.stephengold:SkyControl:1.0.5'

    // select one version of LWJGL
    testRuntimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmeVersion // LWJGL 3.x

    testRuntimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmeVersion
    testRuntimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmeVersion
    
    // Logging
    testImplementation 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'org.slf4j:slf4j-simple:1.7.32'
    
    // libraries related to the Lemur GUI and Groovy:
    testImplementation 'com.simsilica:lemur:1.16.0'
    testImplementation 'com.simsilica:lemur-props:1.2.0'
    testImplementation 'com.simsilica:lemur-proto:1.13.0'
    testRuntimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.16'
    
    testImplementation files("libs/Boost.jar")
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}

publishing {
    publications {
        lib(MavenPublication) {
            groupId = "${group}"
            artifactId = 'codex-particle-vfx'
            version = "${version}"

            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}